
apply plugin: 'android-library'
apply plugin: 'maven-publish'
apply plugin: 'maven'

dependencies {
    compile 'com.android.support:support-v4:19.0.+'
    compile 'com.squareup.okhttp:okhttp:1.2.1'
    compile 'com.intellij:annotations:9.0.4'
}

group 'it.sephiroth.android'
version '2.1.2-PORT'

android {
    compileSdkVersion 19
    buildToolsVersion "18.1.1"

    defaultConfig {
        versionCode 1
        versionName version
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['../picasso/src/main/java']
            resources.srcDirs = ['../picasso/src/main/java']
            aidl.srcDirs = ['../picasso/src/main/java']
            renderscript.srcDirs = ['../picasso/src/main/java']
        }
    }
}

project.ext {
    outJar = rootProject.name + '-' + project.version + '.jar'
}

task clearJar(type: Delete) {
    delete 'build/libs/' + project.ext.outJar
}

task makeJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename ('classes.jar', project.ext.outJar)
}

makeJar.dependsOn 'clearJar'
build.dependsOn 'makeJar'
clean.dependsOn 'clearJar'


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://$System.env.HOME/.m2/repository/")
            pom.project {
                version project.version
                group project.group
                artifactId rootProject.name
                inceptionYear '2013'
                description 'A powerful image downloading and caching library for Android. Original library at https://github.com/square/picasso'
                url 'https://github.com/sephiroth74/picasso'
                licenses {
                    license {
                        name 'Apache Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'square'
                        name 'Square'
                    }
                    developer {
                        id 'sephiroth74'
                        name 'Alessandro Crugnola'
                    }
                }
                organization {
                    name 'Square, Inc.'
                    url 'http://squareup.com'
                }                
            }
        }
    }
}
